* ADC Library
* Author: Kareem Ahmad

.include lib/adi.lib

* Models
.model sw_ideal_n SW(RON=0.1 VT=0.5)
.model sw_ideal_p SW(RON=0.1 VT=-0.5)
.model auto_adc adc_bridge(in_low  = {vmid-.1}  in_high  = {vmid+.1})
.model auto_dac dac_bridge(out_low = {vss}      out_high = {vdd})

* Ramp Generator
.subckt RAMP_GEN vref vo reset enable vdd vss r=100 c=1u; res=100 cap=1u
    srst vn   vo  reset  vss sw_ideal_n
    *sin  vref vin enable vss sw_ideal_n

    xamp vss   vn vdd vss vo AD8648
    r1   vref  vn  {r}; 100
    c1   vn    vo  {c}; 500n
.ends

* Generic ADC Core
.subckt ADC_CORE vin vref comp_out reset enable vdd vss r=100 c=1u
    xcomp ramp_out vin vdd vss comp_out AD8648
    xramp vref ramp_out reset enable vdd vss RAMP_GEN r={r} c={c}
.ends

* Generic ADC
.subckt ADC vin vref clk resetn restart v7 v6 v5 v4 v3 v2 v1 v0 vdd vss vdd=3.3 vss=0 r=100 c=1u
    .param vmid = {(vdd-vss)/2+vss}

    * Analog Components
    xcore vin vref comp_out restart enable vdd vss ADC_CORE r={r} c={c}

    * Digital Components
    a_dig_ctrl [d_clk d_resetn d_comp_out d_start_over] [d_ramp_reset d_enable d_busy d_valid d7 d6 d5 d4 d3 d2 d1 d0] null dmodel
    .model dmodel d_cosim simulation="adc.so"

    * Interface
    abridge_a [clk resetn comp_out restart] [d_clk d_resetn d_comp_out d_start_over] auto_adc
    abridge_d [d_ramp_reset d_enable d_busy d_valid d7 d6 d5 d4 d3 d2 d1 d0] [rreset enable busy valid v7 v6 v5 v4 v3 v2 v1 v0] auto_dac
    .model auto_adc adc_bridge(in_low  = {vmid-.1}  in_high  = {vmid+.1})
    .model auto_dac dac_bridge(out_low = {vss}      out_high = {vdd})
.ends
